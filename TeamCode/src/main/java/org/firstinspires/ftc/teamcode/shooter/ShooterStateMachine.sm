%class ShooterController
%package org.firstinspires.ftc.teamcode.shooter
// http://smc.sourceforge.net/
%start ShooterStateMachine::Deactivated

%map ShooterStateMachine
%%
    Deactivated
        Entry {
            stopLoaderPully();
            setShooterActivation(false);
        }
    {
        evActivate Activating {}
    }
    Activating
        Entry {
            setShooterActivation(true);
        }
    {
        evShooterReady ReadyToShoot {}
        evDeactivate Deactivated {}
    }
    Reload
        Entry{
            stopLoaderPully();
            startTimer(1000);
        }
    {
        evTimeout ReadyToShoot { }
        evDeactivate Deactivated { }
    }

    ReadyToShoot
    {
        evShoot Shooting {}
        evDeactivate Deactivated {}
    }

    Shooting
        Entry {
            startLoaderPully();
            startTimer(1250);
        }
    {
        // After timeout if shooter is already ready then go to ReadyToShoot directly
        // First stop the loader pully
        evTimeout [ctxt.isShooterReady()] ReadyToShoot { stopLoaderPully();  }
        // If shooter is not ready after the timeout, then go back to Reload to
        // wait until the wheels have spun back up
        evTimeout [!ctxt.isShooterReady()] Reload { }
        evDeactivate Deactivated {}
    }

%%